*** 
*** A train system consists of a state, a distance, a speed and an acceleration. 
*** This train is on a circular track of length 5000 after moving 5000 it reaches the beginning.
*** It has 4 states corresponding to accelerating, decelerating, stopped  and full speeds.
***   
*** In this version the non-determinism  where by messages can be interleaved 
*** has been removed.
***
*** 
***   

(tomod DISCRETE-SINGLE-TRAIN is protecting NAT-TIME-DOMAIN .
       protecting MSGS .

  sort TrainState .
  ops  cons acc stop break :  -> TrainState [ctor] .

  class Train | state : TrainState, dist : Nat, speed : Nat, ac : Nat,  ma : Nat, tseg : Nat , maxspeed : Nat, route : Nat .

 


  sort MyConfig .
  subsort ObjectConfiguration < MyConfig .
  subsort EmptyConfiguration < MyConfig .  
  var MyREST : MyConfig .

  op dma : Nat Nat -> Nat .
  op bd : Nat -> Nat .
  op trackseg : Configuration -> Configuration . 

  vars N,N1, N2,M1,M2 : Nat .
  vars P : NzNat . 
  vars DT,MA,S,A,MAX : Nat .
  vars O,O1,O2 : Oid .
  var OCREST : ObjectConfiguration .
  var REST : Configuration . 

  ceq dma(DT,MA) = MA monus DT if DT <= MA .
  ceq dma(DT,MA) = (tlength monus DT) + MA if MA < DT .
 
  eq bd(S) = (S * S) div 2 .

*** The following way of detecting a transition between track segments is inefficient and could be improved.

  crl [transn] : trackseg(< O : Train | dist : DT, tseg : N, route : N2 >) => < O : Train | dist : DT, tseg : N + 1 > trans(DT div tseglength) if N < (DT div tseglength) and (N2 == 1 or not (N == 2)) . 
  crl [transn2] : trackseg(< O : Train | dist : DT, tseg : 2, route : N2 >) => < O : Train | dist : DT, tseg : 5 > trans(5) if 2 < (DT div tseglength)  and N2 == 2 .
  crl [transn2] : trackseg(< O : Train | dist : DT, tseg : N, route : N2 >) => < O : Train | dist : DT, tseg : ((DT div tseglength) + 2) > trans((DT div tseglength) + 2) if N < (DT div tseglength) + 2 and (N2 == 2 and 2 < N) .  
  crl [transn3] : trackseg(< O : Train | dist : DT, tseg : N, route : N2 >) => < O : Train | dist : DT, tseg : N + 1 > trans(DT div tseglength) if N < (DT div tseglength) and N2 == 1 . 
  crl [transn4] : trackseg(< O : Train | dist : DT, tseg : N >) => < O : Train | dist : DT, tseg : N >  if (DT div tseglength) <= N  . 
 
*** Do we need these two rules??? I think its possible to simplify them and remove one
 crl [recma] : grantma(O1,N) < O1 : Train | dist : DT, ma : MA > => < O1 : Train | ma : N > if   MA < N or N < DT  .
 crl [recma2] : grantma(O1,N) < O1 : Train | dist : DT, ma : MA > => < O1 : Train | ma : MA > if  N <= MA and DT <= N  .

*** The following rewrite rules capture the physical behaviour of the train.

  crl [incspeed] : delta(< O : Train | state : acc,  dist : DT, speed : S, ac : A, ma : MA , maxspeed : MAX >)   => pos(O,DT) trackseg(< O : Train |  dist : (DT + S), speed : S + A >)  if S + A < MAX and  bd(S + A) <= dma(DT + S, MA) and (DT + S) < tlength . 


  crl [maxspeed] : delta(< O : Train | state : acc, dist : DT, speed : S, ac : A, ma : MA, maxspeed : MAX >)   => pos(O,DT) trackseg(< O : Train | state : cons, dist : (DT + S) , speed : MAX >)   if MAX <= S + A and bd(MAX) <= dma(DT + S , MA) and (DT + S) < tlength .   
  crl [steadyspeed] : delta(< O : Train | state : cons, dist : DT, speed : S, ac : A, ma : MA >)  => pos(O,DT) trackseg(< O : Train |  dist : (DT + S)  >)  if bd(S + A)  <= dma(DT + S, MA) and (DT + S) < tlength .

*** if the distance to the MA in the next instance of time is less than or eq to the breaking distance the train stops
 
  crl [accbreak] : delta(< O : Train | state : acc, dist : DT, speed : S, ac : A, ma : MA >) =>   reqma(O)pos(O,DT) trackseg(< O : Train | state : break, dist : (DT + S) , speed : S monus A >)   if  dma(DT + S, MA)  <  bd(S + A) and (DT + S) < tlength .

  crl [consbreak] : delta(< O : Train | state : cons, dist : DT, speed : S, ac : A, ma : MA >)   =>  reqma(O) pos(O,DT) trackseg(< O : Train | state : break, dist : (DT + S), speed : S monus A >)    if  dma(DT + S, MA)  <  bd(S + A) and (DT + S) < tlength .
  crl [breaking] : delta(< O : Train | state : break, dist : DT, speed : S, ac : A, ma : MA >)   => reqma(O) pos(O,DT) trackseg(< O : Train | state : break, dist : (DT + S), speed : S monus A >)   if 0 < S monus A and dma(DT + S, MA)  <  bd(S + A) and (DT + S) < tlength .
  crl [stopping] : delta(< O : Train | state : break, dist : DT, speed : S, ac : A, ma : MA >)  => reqma(O) pos(O,DT) trackseg(< O : Train | state : stop,  speed : 0 >)   if 0 == S monus A and dma(DT + S, MA)  <  bd(S + A)  and (DT + S) < tlength .
  crl [stopped] :  delta(< O : Train | state : stop, dist : DT, speed : S, ac : A, ma : MA >)  => reqma(O) pos(O,DT) < O : Train | >  if  dma(DT + S, MA)  <  bd(S + A) .
  crl [breakacc] : delta(< O : Train | state : break, dist : DT, speed : S, ac : A, ma : MA >)  =>  pos(O,DT) trackseg(< O : Train | state : acc, dist : (DT + S ), speed : S >)   if  bd(S + A)   <= dma((DT + S) , MA) and (DT + S) < tlength .
  crl [stopacc] :  delta(< O : Train | state : stop, dist : DT, speed : S, ac : A, ma : MA >)  =>  pos(O,DT) trackseg(< O : Train | state : acc, dist : (DT + S), speed : S >)    if  bd(S + A)   <= dma((DT + S) , MA) and (DT + S) < tlength .
  crl [leave] :  delta(< O : Train | dist : DT, speed : S, ac : A, ma : MA, route : N >)   =>  pos(O,DT)leave(N)   if  tlength <= (DT + S)  .

endtom)


(trew {< train1 : Train | state : acc, dist : 0, speed : 0, ac : 1, ma : 300, tseg : 0 , maxspeed : 5 >} in time <= 100 .)
